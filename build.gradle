// Build plugin repositories and dependencies
buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      name = 'sonatype-nexus'
      url = 'https://oss.sonatype.org/content/groups/public/'
    }
  }
  dependencies {
    classpath 'net.saliman:gradle-cobertura-plugin:2.6.1'
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
    classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
  }
}

plugins {
  id 'java-library'
  id 'maven'
  id 'net.minecrell.licenser' version '0.4.1'
}

apply plugin: 'cobertura'
apply plugin: 'com.github.kt3k.coveralls'

group 'org.spongepowered'
version '1.0.1-SNAPSHOT'
description 'Noise generation library for Java, based on the libnoise C++ library.'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
  options.deprecation = true
  options.encoding = 'UTF-8'
  options.compilerArgs += ['-Xlint:all', '-Xlint:-path']
}

tasks.withType(Javadoc) {
  options.addStringOption('Xdoclint:none', '-quiet')
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/groups/public/'
  }
}

dependencies {
  testCompile 'junit:junit:4.12'
}

license {
  header project.file('HEADER.txt')
  include '**/*.java'
  newLine false
}

cobertura.coverageFormats = ['html', 'xml']

task javadocJar(type: Jar) {
  classifier 'javadoc'
  from javadoc
}

task sourcesJar(type: Jar) {
  classifier 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives sourcesJar
}

processResources {
  from(rootProject.rootDir) {
    include 'LICENSE.txt'
  }
}

if(project.hasProperty('spongeRepo') && project.hasProperty('spongeUsername') && project.hasProperty('spongePassword')) {
  uploadArchives {
    repositories.mavenDeployer {
      repository(url: spongeRepo) {
        authentication(userName: spongeUsername, password: spongePassword)
      }

      snapshotRepository(url: spongeRepo) {
        authentication(userName: spongeUsername, password: spongePassword)
      }

      pom.project {
        name project.name
        description project.description
        url 'https://github.com/SpongePowered/noise'

        scm {
          connection 'scm:git:git://github.com/flow/noise.git'
          developerConnection 'scm:git:ssh://git@github.com:flow/noise.git'
          url 'https://github.com/flow/noise'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://tldrlegal.com/l/mit'
            distribution 'repo'
          }
        }

        organization {
          name 'SpongePowered'
          url 'https://github.com/SpongePowered'
        }

        developers {
          developer {
            id 'DDoS'
            name 'Aleksi Sapon'
            email 'qctechs@gmail.com'
          }
          developer {
            id 'kitskub'
            name 'Jack Huey'
            email 'kitskub@gmail.com'
          }
          developer {
            id 'Wolf480pl'
            name 'Wolf480pl'
            email 'wolf480@interia.pl'
          }
          developer {
            id 'lukespragg'
            name 'Luke Spragg'
            email 'the@wulf.im'
          }
        }
      }
    }
  }
}
